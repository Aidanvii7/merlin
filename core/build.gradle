apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'com.android.library'

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.github.kevinsawicki:http-request:4.0'
}

android {
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    compileSdkVersion 19
    buildToolsVersion "20.0.0"
}

group = 'com.novoda'
ext.packaging = 'aar'

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

uploadArchives {
    configuration = configurations.archives
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {

                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername,
                            password: sonatypePassword)
                }

                pom.project {
                    artifactId 'merlin'
                    name 'Merlin'
                    description 'Observes network connection status & gives callbacks'
                    packaging 'aar'
                    url 'https://github.com/novoda/merlin'
                    inceptionYear '2013'

                    scm {
                        url 'https://github.com/novoda/merlin'
                        connection 'scm:https://github.com/novoda/merlin.git'
                        developerConnection 'scm:git@github.com:novoda/merlin.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'ouchadam'
                            name 'Adam Brown'
                        }

                        developer {
                            id 'blundell'
                            name 'Paul Blundell'
                        }

                        developer {
                            id 'Electryc'
                            name 'Andrei Catinean'
                        }
                    }
                }
            }
        }
    }
}
